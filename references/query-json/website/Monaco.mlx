type self
type monaco
type config = { monaco : monaco }
type loader = { config : config -> unit; init : unit -> unit Js.Promise.t }
type worker
type monacoEnv
type environment = { getWorker : unit -> string -> worker }

external loader : loader = "loader" [@@mel.module "@monaco-editor/react"]
external monaco : monaco = "monaco-editor" [@@mel.module]

external json_worker : unit -> worker = "default"
[@@mel.module "monaco-editor/esm/vs/language/json/json.worker?worker"]
[@@mel.new]

external editor_worker : unit -> worker = "default"
[@@mel.module "monaco-editor/esm/vs/editor/editor.worker?worker"] [@@mel.new]

external create_env : self -> environment -> monacoEnv = "MonacoEnvironment"
[@@mel.set]

let _ =
  let getWorker _ label : worker =
    match label with "json" -> json_worker () | _ -> editor_worker ()
  in
  let _env = create_env [%mel.raw {|self|}] { getWorker } in
  let () = loader.config { monaco } in
  loader.init () |> Js.Promise.then_ (fun _ -> Js.Promise.resolve ()) |> ignore

type mode = Text | Json

let mode_to_string = function Text -> "text" | Json -> "json"

type padding = { bottom : int; top : int }
type minimap = { enabled : bool }

type options = {
  fontSize : int;
  fontFamily : string;
  glyphMargin : bool;
  lineNumbersMinChars : int;
  padding : padding;
  minimap : minimap;
  wordWrap : string;
  selectionHighlight : bool;
  occurrencesHighlight : bool;
  matchBrackets : string;
  bracketPairColorizationEnabled : bool;
      [@mel.as "bracketPairColorization.enabled"]
  readOnly : bool;
}

module External = struct
  external make :
    language:string ->
    height:string ->
    value:string ->
    onChange:(string -> unit) ->
    ?className:string ->
    ?style:ReactDOM.Style.t ->
    theme:string ->
    options:options ->
    React.element = "default"
  [@@mel.module "@monaco-editor/react"] [@@react.component]
end

type on_change = Write of (string -> unit) | Read_only

let is_read_only = function Write _ -> false | Read_only -> true

let[@react.component] make ~(value : string)
    ~on_change:(on_change_value : on_change) ~(mode : mode) =
  let on_change =
    match on_change_value with
    | Read_only -> fun _ -> ()
    | Write handler -> handler
  in
  let options =
    {
      fontSize = 16;
      fontFamily = "IBM Plex Mono";
      glyphMargin = false;
      lineNumbersMinChars = 3;
      padding = { top = 8; bottom = 8 };
      minimap = { enabled = false };
      wordWrap = "on";
      selectionHighlight = false;
      occurrencesHighlight = false;
      matchBrackets = "never";
      bracketPairColorizationEnabled = false;
      readOnly = is_read_only on_change_value;
    }
  in
  <External
    value
    onChange=on_change
    options
    theme="vs-dark"
    language=(mode_to_string mode)
    height="100%"
  />
