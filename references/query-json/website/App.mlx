let initial_query = ".store"

let initial_json =
  {|{
  "store": {
    "books": [
      {
        "category": "reference",
        "author": "Nigel Rees",
        "title": "Sayings of the Century",
        "price": 8.95
      },
      {
        "category": "fiction",
        "author": "Evelyn Waugh",
        "title": "Sword of Honour",
        "price": 12.99
      },
      {
        "category": "fiction",
        "author": "Herman Melville",
        "title": "Moby Dick",
        "isbn": "0-553-21311-3",
        "price": 8.99
      },
      {
        "category": "fiction",
        "author": "J. R. R. Tolkien",
        "title": "The Lord of the Rings",
        "isbn": "0-395-19395-8",
        "price": 22.99
      }
    ]
  }
}
|}

type t = {
  query : string;
  json : string option;
  copied : bool;
  isModalOpen : bool;
}

type actions =
  | Update_query of string
  | Update_json of string
  | Set_copied of bool
  | Set_modal_open of bool

let reduce state = function
  | Update_query query -> { state with query }
  | Update_json json -> { state with json = Some json }
  | Set_copied copied -> { state with copied }
  | Set_modal_open isModalOpen -> { state with isModalOpen }

module Query_params = struct
  open Melange_json.Primitives

  type t = { query : string; json : string option } [@@deriving json]

  let decode json = of_json json
  let encode t = to_json t
  let toString t = t |> encode |> Melange_json.to_string
  let fromString str = str |> Melange_json.of_string |> decode
  let toHash state = state |> toString |> Base64.encode
end

module Option = struct
  include Option

  (* Option.bind with pipe-last friendly *)
  let bind f o = match o with None -> None | Some v -> f v
end

module Query_json = struct
  type jsoo_result = {
    first : int; [@mel.as "0"]
    second : string; [@mel.as "1"]
  }

  let to_result (res : jsoo_result) =
    match res.first with
    | 0 -> Ok res.second
    | 1 -> Error res.second
    | _ -> Error "Invalid result from QueryJsonJs"

  let query_json_run : string -> string -> jsoo_result =
    [%mel.raw
      {| function (query, json) { return window['query-json'].run(query, json); }|}]

  let run query json = to_result (query_json_run query json)
end

module Clipboard = struct
  external write_text : string -> unit Js.Promise.t = "writeText"
  [@@mel.scope "navigator", "clipboard"]
end

let[@react.component] make () =
  let state_from_hash =
    Router.get_hash () |> Option.bind Base64.decode
    |> Option.map Query_params.fromString
  in

  let initial_state =
    match state_from_hash with
    | Some { query; json } ->
        { query; json; copied = false; isModalOpen = false }
    | None ->
        {
          query = initial_query;
          json = Some initial_json;
          copied = false;
          isModalOpen = false;
        }
  in

  let state, dispatch = React.useReducer reduce initial_state in

  let on_query_change = fun value -> dispatch (Update_query value) in
  let on_json_change = fun value -> dispatch (Update_json value) in

  let output =
    React.useMemo2
      (fun () ->
        match (state.json, state.query) with
        | Some _, "" | None, _ -> None
        | Some json, _ -> Some (Query_json.run state.query json))
      (state.json, state.query)
  in

  let on_share_click =
    React.useCallback2
      (fun _ ->
        let hash =
          Query_params.toHash { query = state.query; json = state.json }
        in
        match hash with
        | Some hash ->
            let url =
              Web.window |> Web.Window.location |> Web.Location.origin
            in
            let fullUrl = url ^ "#" ^ hash in
            let _promise = Clipboard.write_text fullUrl in
            dispatch (Set_copied true);
            let _timeout =
              Js.Global.setTimeout
                ~f:(fun () -> dispatch (Set_copied false))
                2000
            in
            ()
        | None -> Printf.eprintf "Error encoding state")
      (state.query, state.json)
  in

  let on_examples_click = fun _ -> dispatch (Set_modal_open true) in
  let on_close_modal = fun () -> dispatch (Set_modal_open false) in
  let on_select_example =
   fun query json ->
    dispatch (Update_query query);
    dispatch (Update_json json)
  in

  <div className="flex flex-col min-h-screen bg-background">
    <Header>
      <Button
        kind=Secondary size=Sm on_click=on_share_click className="min-w-[110px]">
        (match state.copied with
        | true ->
            <span className="flex items-center gap-2">
              <Icons.Check size=Small /> (React.string "Copied!")
            </span>
        | false ->
            <span className="flex items-center gap-2">
              <Icons.Link2 size=Small /> (React.string "Copy unique URL")
            </span>)
      </Button>
    </Header>
    <TextInput
      value=state.query
      on_change=on_query_change
      on_examples_click
      placeholder="Type your filter (e.g., '.store.books[0]')"
    />
    <Editors
      json_value=(Option.value state.json ~default:"") on_json_change output
    />
    <Footer />
    <Modal_examples
      is_open=state.isModalOpen on_close=on_close_modal on_select_example
    />
  </div>
